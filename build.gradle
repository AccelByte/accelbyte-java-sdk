plugins {
    id 'java'
    id 'maven-publish'
    id 'checkstyle'
}

apply from: "repositories.gradle"
apply from: "publish.gradle"
apply from: "shared-dependencies.gradle"

import groovy.json.JsonSlurper
def jsonSlurper = new JsonSlurper()
def versionJson = jsonSlurper.parse file('version.json')

group = 'net.accelbyte.sdk'
version = "${version != 'unspecified' ? version : versionJson.version}"

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly sharedDependencies.lombok
    annotationProcessor sharedDependencies.lombok

    implementation sharedDependencies.okhttp
    implementation sharedDependencies.apacheHttpClient
    implementation sharedDependencies.jackson.databind
    implementation sharedDependencies.apacheCommonIO

    testCompileOnly sharedDependencies.lombok
    testAnnotationProcessor sharedDependencies.lombok
    testRuntimeOnly sharedDependencies.junitEngine
    testImplementation sharedDependencies.junitApi
}

checkstyle {
    configFile = project.file('config/checkstyle/checkstyle.xml')
    // https://stackoverflow.com/a/57141028
    //ignoreFailures = false
    //maxWarnings = 0
}

test {
    useJUnitPlatform {
        includeTags('unit-test')
    }
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

task testIntegration(type: Test) {
    useJUnitPlatform {
        includeTags('integration-test')
    }
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}
