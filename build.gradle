plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'io.freefair.lombok' version '6.4.2'
}

apply from: "repositories.gradle"
apply from: "publish.gradle"
apply from: "shared-dependencies.gradle"

import groovy.json.JsonSlurper
def jsonSlurper = new JsonSlurper()
def versionJson = jsonSlurper.parse file('version.json')

group = 'net.accelbyte.sdk'
version = "${version != 'unspecified' ? version : versionJson.version}"

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation sharedDependencies.okhttp
    implementation sharedDependencies.apacheHttpClient
    implementation sharedDependencies.jackson.databind
    implementation sharedDependencies.apacheCommonIO
    
    testImplementation sharedDependencies.junitApi
    testImplementation  sharedDependencies.junitParams
    testRuntimeOnly sharedDependencies.junitEngine
}

java {
    //withJavadocJar()
    withSourcesJar()
}

task testCore(type: Test) {
    useJUnitPlatform {
        includeTags('test-core')
    }
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

task testIntegration(type: Test) {
    useJUnitPlatform {
        includeTags('test-integration')
    }
    reports {
        junitXml.enabled = true
        html.enabled = false
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'jd'
}
